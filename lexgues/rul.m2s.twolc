!
! Copyright Kimmo Koskenniemi, 2012, 2016
!
Alphabet
  a b c d e f g h i j k l m n o p q r s t u v w x y z å ä ö
!  A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Å Ä Ö
  à é ü
 %{ao%}:a %{ao%}:o %{aoe%}:a %{aoe%}:o %{aoe%}:e
 %{aoØe%}:a %{aoØe%}:o %{aoØe%}:Ø %{aoØe%}:e
 %{aä%}:a %{aä%}:ä %{aäØ%}:a %{aäØ%}:ä %{aäØ%}:Ø
 %{aäØe%}:a %{aäØe%}:ä %{aäØ%}:Ø %{aäØe%}:e
 %{aØ%}:a %{aØ%}:Ø %{aØe%}:a %{aØe%}:Ø %{aØe%}:e
 %{dlnrtØ%}:d %{dlnrtØ%}:l %{dlnrtØ%}:n %{dlnrtØ%}:r %{dlnrtØ%}:t %{dlnrtØ%}:Ø
 %{eiØ%}:e %{eiØ%}:i %{eiØ%}:Ø %{eØ%}:e %{eØ%}:Ø
 %{ie%}:i %{ie%}:e
 %{i%}:i
 %{ij%}:i %{ij%}:j
 %{iØ%}:i %{iØ%}:Ø
 %{nlrs%}:n %{nlrs%}:l %{nlrs%}:r %{nlrs%}:s
 %{kg%}:k %{kg%}:g %{kj%}:k %{kj%}:j %{kv%}:k %{kv%}:v
 %{kØ%}:k %{kØ%}:Ø  %{kØ%'%}:k %{kØ%'%}:Ø  %{k%Ø%'%}:%'
 %{oö%}:ö %{oö%}:o
 %{pm%}:p %{pm%}:m %{pv%}:p %{pv%}:v %{pØ%}:p %{pØ%}:Ø
 %{td%}:t %{td%}:d %{tds%}:t %{tds%}:d %{tds%}:s %{tl%}:t %{tl%}:l
 %{tls%}:t %{tls%}:l %{tls%}:s %{tn%}:t %{tn%}:n %{tns%}:t %{tns%}:n %{tns%}:s
 %{tr%}:t %{tr%}:r %{trs%}:t %{trs%}:r %{trs%}:s %{tØ%}:t %{tØ%}:Ø
 %{uy%}:y %{uy%}:u %{uØ%}:u %{uØ%}:Ø
 %{V%}:a %{V%}:e %{V%}:i %{V%}:o %{V%}:u %{V%}:y %{V%}:ä %{V%}:ö
 %{VØ%}:a %{VØ%}:e %{VØ%}:i %{VØ%}:o %{VØ%}:u %{VØ%}:y %{VØ%}:ä %{VØ%}:ö %{VØ%}:Ø
 %{yØ%}:y %{yØ%}:Ø
 %{äØ%}:ä %{äØ%}:Ø %{äØe%}:ä %{äØe%}:Ø %{äØe%}:e
  %{C%}:Ø
 %{Øt%}:Ø %{Øt%}:t %{nt%}:n %{nt%}:t 
!  #:0 %+:0
 %_:Ø
 %|:Ø
 §:Ø %-
 %=:Ø
 %+1PL %+1SG %+2PL %+2SG %+3PL %+3SG %+4PE %+A %+ABE %+ABL %+ACT %+ADE %+ALL %+CMP %+CMT %+COND %+DVMA %+DVMI %+ELA %+ESS %+GEN %+ILL %+IMPV %+INE %+INF1 %+INF2 %+INS %+MAN %+N %+NEG %+NOM %+PAST %+PCP1 %+PCP2 %+PCP3 %+PL %+PL1 %+PL2 %+PL3 %+POS %+POTN %+PRES %+PSS %+PTV %+SG %+SG1 %+SG2 %+SG3 %+SUP %+TRA %+V
  ;

Rule-variables
  VV XX YY ;

Sets
  Vowel = a e i o u y ä ö     !  A E I O U Y Ä Ö
          %{ao%} %{aoe%} %{aoØe%} %{aä%} %{aØe%} %{aØ%} %{aäØ%} %{aäØe%}
          %{eiØ%} %{eØ%} %{i%} %{ie%} %{iØ%} %{ij%}
          %{oö%} %{uy%} %{uØ%} %{V%} %{VØ%} %{äØe%} %{äØ%} ; 
  Cons = b c d f g h j k l m n p q r s t v w x z
         %{C%} %{kØ%} %{kg%} %{kj%} %{kv%} %{kØ%'%}
         %{nlrs%} %{nt%}
         %{pm%}  %{pv%} %{pØ%}
         %{td%}  %{tds%} %{tl%} %{tls%} %{tn%} %{tns%} %{tr%} %{trs%} %{tØ%}
         %{dlnrtØ%} %{Øt%} ;
  oØ = o Ø ;

Definitions
  Closed = (%{ij%}: | %{i%}: | i)  (%{eØ%}:Ø | %{eiØ%}:Ø) Cons: [ (%{eØ%}:Ø | %{eiØ%}:Ø) Cons: | .#. | #:Ø | §:Ø | %- | %+V: | %+N:] ;

Rules

"Weakening except k~Ø~'"
%{kg%}:g | %{kj%}:j | %{kv%}:v |
%{pm%}:m | %{pv%}:v | %{pØ%}:Ø |
%{td%}:d | %{tl%}:l | %{tn%}:n | %{tr%}:r | %{tØ%}:Ø <=>  _  Vowel: Closed ;

!               ~ h a r a v o i {nt}:t {nlrs}:s e m m e

!"kä<d>en jäl<l>en kan<n>en or<r>en"
!%{tds%}:d | %{tls%}:l | %{tns%}:n | %{trs%}:r <=> _  :Vowel Closed ;
!                                           except _ (:Ø)* [%{i%}: | %{ij%}:] ;
!
!"kä<s>i jäl<s>i kan<s>i or<s>i"
!%{tds%}:s | %{tls%}:s | %{tns%}:s | %{trs%}:s <=> _ (:Ø)* [%{i%}: | %{ij%}:] ;

"NOT kä<t>en jäl<t>en kan<t>en or<t>en"
%{tds%}:t | %{tls%}:t | %{tns%}:t | %{trs%}:t /<= _  Vowel: Closed ;

"kä<d>en jäl<l>en kan<n>en or<r>en"
%{tds%}:d | %{tls%}:l | %{tns%}:n | %{trs%}:r => _  Vowel: Closed ;

"kä<s>i jäl<s>i kan<s>i or<s>i"
%{tds%}:s | %{tls%}:s | %{tns%}:s | %{trs%}:s => _ Vowel:Ø [%{i%}: | %{ij%}:] ;

"pa<>on"
 %{kØ%}:Ø <=>   _  :Vowel Closed ;

"vaa<'>an"
%{kØ%'%}:%' <=> :Vowel :VV _ :VV Closed ;
                where VV in (a e i o u y ä ö) ;

"vaa<>oissa"
%{kØ%'%}:k /<= _ :Vowel Closed ;

"taloss<a>" 
%{aä%}:a | %{aäØ%}:a | %{aØe%}:a | %{aØe%}:Ø | %{aØe%}:e  => [:a | :o | :u] \[%=: | %|: | %_: | #:]* _ ;
"työss<ä>" 
%{aä%}:ä | %{aäØ%}:ä | %{äØe%}:ä | %{äØe%}:Ø | %{äØe%}:e /<= [:a | :o | :u] \[%=: | %|: | %_: | #:]* _ ;


"rakent<a>vat"
%{aäØe%}:a => [:a | :o | :u] \[%=: | %|: | %_: | #:]* _ ;

"lyhent<ä>vät"
%{aäØe%}:ä => .#. \[:a | :o | :u | #:]* _ ;

"maak<o>" 
%{oö%}:o <=> [:a | :o | :u] \[%=: | %|: | %_: | #:]* _ ;

"kuoll<u>t" 
%{uy%}:u <=> [:a | :o | :u] \[%=: | %|: | %_: | #:]* _ ;

"tul<>in vo<>isin pur<>ra" 
%{eØ%}:Ø | %{eiØ%}:Ø <=>
               _ [%{ij%}: | %{i%}: | i s i | t %{aä%}:] ;
               Vowel: [:l | :n | :r | :s] _ [%{nlrs%}: | %{dlnrtØ%}: |
                                             %{Øt%}: :t |
                                             k %{aä%}: %{aä%}: |
                                             k %{oö%}: %{oö%}:] ;

"t<>eiden v<>ein haravo<>isi"
%{iØ%}:Ø <=> _ (e) [%{ij%}: | %{i%}: | i s i] ;

"s<>oissa s<>oisivat"
%{uØ%}:Ø <=> _ o [%{ij%}: | %{i%}: | i s i] ;

"s<>öin"
%{yØ%}:Ø <=> _ ö [%{ij%}: | %{i%}: | i s i] ;

"kiel<i>" 
%{eiØ%}:i <=> _ [.#. | %=:Ø | §:Ø | #:Ø | %{dlnrtØ%}:Ø e] ;
              except Vowel: [:l | :n | :r | :s] _ %{dlnrtØ%}: e ;

"rist<e>jä sot<>ia kal<o>ja hät<>iä"
 %{ie%}:e | %{aØ%}:Ø | %{aØe%}:Ø | %{aoe%}:o | %{aäØ%}:Ø | %{ao%}:o |
 %{äö%}:ö | %{äØ%}:Ø | %{äØe%}:Ø <=> _ [%{ij%}: | %{i%}:] ;

"haast<o>in haast<>in"
%{aoØe%}:o | %{aoØe%}:Ø => _ %{i%}: ;

"not haast<a>i"
%{aoØe%}:a | %{aäØe%}:a | %{aäØe%}:ä /<= _ %{i%}: ;

"pah<e>mpi puoll<e>taan"
%{aØe%}:e | %{aoe%}:e | %{aoØe%}:e | %{äØe%}:e | %{aäØe%}:e <=>
              _ [m %{pm%}: |
                 %{Øt%}: :t |
                 %{dlnrtØ%}: %{C%}: %{aä%}:] ;

"mene<e>"
%{VØ%}:VV => :VV _ ;
          where VV in (a e i o u y ä ö) ;

"maah<a>n"
%{V%}:VV <=> :VV (h) _ ;
          where VV in (a e i o u y ä ö) ;

"ma<>issa" 
%{VØ%}:Ø <=> _ [%{ij%}: | %{i%}: | i s i] ;
             [%{i%}: | i s i | %{VØ%}: | :u :o | :y :ö |
              :i :e | :a :i | :o :i | :ö :i | :ä :y] _ ;
             

"talo<j>a"
%{ij%}:j <=> :Vowel _ :Vowel ;

!"salat<>a"
!%{dlnrtØ%}:Ø <=> [:t] _ ;
!               s a l a t {dlnrtØ}:Ø {aä}:a
!               p u n o {dlnrtØ}:Ø {aä}:a k s e n i
!               p u n o {dlnrtØ}:Ø {aä}:a
!               p u n o {dlnrtØ}:Ø e s {aä}:a
!               h i o  {dlnrtØ}:Ø {aä}:a

"tuo<d>a näh<d>ä"
!%{dlnrtØ%}:d <=> :Vowel [:Vowel | h] _ ;
%{dlnrtØ%}:d <=> :Vowel h _ ;
                  [[:a|:o|:u|:ö] :i | :a :a | :ä :ä |
		  :u :o | :y :ö | :i :e | :ä :y] _ ;

"nuol<l>a men<n>ä pur<r>a"
%{dlnrtØ%}:XX <=> :XX (%{eØ%}:Ø) _ ; where XX in (l n r) ;

"juos<t>a puno<t>aan"
%{dlnrtØ%}:t <=> :s (:Ø) _ ;
                 Cons: :Vowel _ [%{C%}: %{aä%}: |
                                 t %{aä%}:] ;

"nuol<l>et men<n>et pur<r>ette juos<s>ee"
%{nlrs%}:XX <=> :Vowel :XX (:Ø) _ [:e | %{uy%}:] ;
                 where XX in (l r s) ;

"kudo<t>tiin pur<>tiin"
%{Øt%}:t <=> Cons: :Vowel _ ;

"halu<n>nut"
 %{nt%}:n <=> _ %{nlrs%}: ;
